#CONFIG
language=GLSL
cull=FRONT
front=CLOCKWISE
fill=SOLID
depth=LESS
blend=ADD
instanceOffset=2
order=0
#CONFIG_END

#VERTEX
#version 450

struct GlobalTileData
{
	vec4 globalColor;
};

struct CameraData
{
	mat4 viewProjection;
    vec4 eye;
};

layout(push_constant) uniform block
{
    GlobalTileData globalTileData;
	CameraData cameraData;
};

layout (location = 0) in vec3 position;
layout (location = 1) in vec2 texcoord;

layout (location = 2) in vec2 instPosition;
layout (location = 3) in vec2 instTexcoord;
layout (location = 4) in vec3 instColor;
layout (location = 5) in uint texIndex;

layout (location = 0) out vec2 outTexcoord;
layout (location = 1) out vec3 outColor;
layout (location = 2) flat out uint outTexIndex;

void main()
{
    vec4 worldPosition = vec4(position.xy + instPosition.xy, position.z, 1.0);
    gl_Position = cameraData.viewProjection * worldPosition;
    outTexcoord = texcoord + instTexcoord;
    outColor = instColor * globalTileData.globalColor.rgb;
    outTexIndex = texIndex;
}
#VERTEX_END

#FRAGMENT
#version 450
#extension GL_EXT_nonuniform_qualifier : require

layout (location = 0) in vec2 texcoord;
layout (location = 1) in vec3 color;
layout (location = 2) flat in uint texIndex;

layout (set = 1, binding = 10) uniform sampler2D globalTextures[];

layout(location = 0) out vec4 outColor;

const uint INVALID_TEXTURE_INDEX = 65535;

void main()
{
    if(texIndex == INVALID_TEXTURE_INDEX) { discard; }

    outColor = texture(globalTextures[texIndex], texcoord) * vec4(color, 1.0);

    if(outColor.a < 0.1) { discard; }
}
#FRAGMENT_END